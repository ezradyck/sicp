**April 16, 2023**

# Exercise 1.16

> Design a procedure that evolves an iterative exponentation process that uses successive squaring and uses a logarithmic number of steps, as does `fast-expt`.

```scheme
(define (fast-expt b n)
    (cond ((= n 0) 1)
          ((even? n) (square (fast-expt b (/ n 2))))
          (else (* b (fast-expt b (- n 1))))))

(define (even? n)
    (= (remainder n 2) 0))
```

## Iterative Process

```scheme
(define (fast-expt b n)
    (define (iter b n a) 
        (cond ((= n 0) a)
              ((even? n) (iter (square b) (/ n 2) a))
              (else (iter b (- n 1) (* a b)))))

    (define (even? n)
        (= (remainder n 2) 0))

    (iter b n 1))
```

## Explanation

If we examine the recursive `fast-expt` process we can see that the only things holding us back from being an iterative process is the `(square ...)` and `(* b ...)` operations that occur before each recursive call.

We can move the call to `square` the result of the recursive call into the recursive call by squaring _b_ before passing it to the next recursive call. 
This can be done because (_b_^(_n_/2))^2 = (_b_^2)^(_n_/2) is known to be true.

The multiplication of the recusive call and _b_ in the case where _n_ is odd can be removed by adding an accumulator parameter, _a_, 
which is initialized as 1 (to handle our base case of _n_ = 0), that is multiplied by _b_ before passing it to the next recursive call.


This procedure now takes constant space as it is tail-recusive, and has a logarithmic time complexity.

