**April 16, 2023**

# Exercise 1.18

> Using the results of exercises 1.16 and 1.17, devise a procedure that generates an iterative process for multiplying two integers in terms of 
> adding, doubling, and halving that uses a logarithmic number of steps.

## Recap

For reference, here is the recursive process procedure that we came up with in exercise 1.17.

```scheme
(define (* a b)
    (cond ((= b 0) 0)
          ((even? b) (* (double a) (halve b)))
          (else (+ a (* a (- b 1))))))

(define (even? n)
    (= (remainder  n 2) 0))

(define (double n)
    (+ n n))

(define (halve n)
    (/ n 2))
```

And here is the iterative process procedure that we came up with in exercise 1.16.

```scheme
(define (fast-expt b n)
    (define (iter b n a) 
        (cond ((= n 0) a)
              ((even? n) (iter (square b) (/ n 2) a))
              (else (iter b (- n 1) (* a b)))))

    (define (even? n)
        (= (remainder n 2) 0))

    (iter b n 1))
```

## Solution

We can pretty easily apply the same transformations to the multiplication procedure that we made to the
exponentation procedure to generate an iterative process.

```scheme
(define (* a b)
    (define (iter a b c)
        (cond ((= b 0) c)
              ((even? b) (iter (double a) (halve b) c))
              (else (iter a (- b 1) (+ a c)))))

    (define (even? n)
        (= (remainder  n 2) 0))

    (define (double n)
        (+ n n))

    (define (halve n)
        (/ n 2))

    (iter a b 0))
```

The major change between the iterative process and the recursive process is that the iterative process
has the additional parameter, _c_, which carries the eventual result.

Initially we pass in 0 as the result, which covers our base case of _b_ = 0, and then every time _b_ is odd we increment _c_ by _a_, which covers all of the cases where _b_ is odd.

At most, the addition to _c_ only occurs twice; once if _b_ is odd to begin with, in which case the next _c_ is equal to `(+ a 0)`, and once more at the end of the recursion, 
in which case _c_ could be 0 (if _b_ were even to begin with), or the initial value of _a_ (if _b_ were odd to begin with.) 

